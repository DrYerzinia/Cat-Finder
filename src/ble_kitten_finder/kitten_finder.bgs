# Kitten Finder V2 Firmware
#
# Beacons for 10 seconds every 2 minutes to conserve battery power
# When connected LED flashs and device beeps every 3 second to assist
# in locating the device
# Able to set custom name so you can identify the cat by the device
# name when scanning
#
# Author: Michael Marques <DrYerzinia@gmail.com>
# Sept 20th 2015

dim read_result
dim port
dim data
dim state
dim bat_pct
dim name_end
dim sr_data(20) # custom scan response data (friendly name for active scanning)
dim psLength
dim psResult
dim cat_name(18)
dim i
dim len

# Boot event listener
event system_boot(major, minor, patch, build, ll_version, protocol_version, hw)
	state = 0
	
	# configure P1.6 and P0.7 as output
	call hardware_io_port_write(1, $40, 0)
	call hardware_io_port_write(0, $90, 0)
	call hardware_io_port_config_direction(1, $40)
	call hardware_io_port_config_direction(0, $90)

	call flash_ps_load($8000)(psResult, psLength, cat_name(0:18))

	if cat_name(0:1) = 0
		cat_name(0:1) = 7	# len
		cat_name(1:1) = $55	# 'U'
		cat_name(2:1) = $6E	# 'n'
		cat_name(3:1) = $6B	# 'k'
		cat_name(4:1) = $6E	# 'n'
		cat_name(5:1) = $6F	# 'o'
		cat_name(6:1) = $77	# 'w'
		cat_name(7:1) = $6E	# 'n'
		call flash_ps_save($8000, 18, cat_name(0:18))
	end if

	# Set Advertisement cats name
	sr_data(0:1) = $13	# ad field length = 19 bytes (0x13)
	sr_data(1:1) = gap_ad_type_localname_complete	 # ad field type = 0x09 (Complete local name)

	i = 0
	len = cat_name(0:1)
	while i < len
		sr_data(2 + i:1) = cat_name(1 + i:1)
		i = i + 1
	end while
	name_end = i + 2

	# write custom device name to local GATT attribute
	call attributes_write(c_device_name, 0, 18, sr_data(2:18))

	# set advertisement interval to 640 mS, all channels
	# units for min max 625 uS
	call gap_set_adv_parameters($400, $400, $07)

	# System started, enable advertising and allow connections
	# Wait 10 seconds and then stop beaconing
	call gap_set_mode(gap_general_discoverable, gap_undirected_connectable)
	#call hardware_set_soft_timer(10 * 32768, 2, 1)

end

event hardware_soft_timer(handle)

	# Transmit for 10 seconds
	if handle = 3 then
		call attributes_write(c_device_name, 0, 18, sr_data(2:18))
		call gap_set_mode(gap_general_discoverable, gap_undirected_connectable)
	#	call hardware_set_soft_timer(10 * 32768, 2, 1)
	end if

	# Stop transmiting
	if handle = 2 then
		call gap_set_mode(0, 0)
		call hardware_set_soft_timer(110*32768, 3, 1)
	end if

	# blink and beep when connected to help user find collar
	if handle = 1 then
		if state then
			# Turn on LED and WAKE
			call hardware_io_port_write(1, $40, $40)
			call hardware_io_port_write(0, $10, $10)
			call hardware_timer_comparator(1, 0, 6, 8000)
			call hardware_timer_comparator(1, 3, 6, 4000)
			state = 0
			call hardware_set_soft_timer(10000, 1, 1)
		else
			# Turn off LED and WAKE
			call hardware_io_port_write(1, $50, 0)
			call hardware_io_port_write(0, $10, 0)
			call hardware_timer_comparator(1, 3, 1, 0)
			state = 1
			call hardware_set_soft_timer(150000, 1, 1)
		end if
	end if

end

# Gets changes to GATT attributes
# Changes cats name if updated
event attributes_value(connection, reason, handle, offset, value_len, value)

	if handle = xcat_name then

		call flash_ps_save($8000, value_len, value(0:value_len))

		cat_name(0:value_len) = value(0:value_len)

		i = 0
		len = cat_name(0:1)
		while i < len & i < value_len
			sr_data(2 + i:1) = cat_name(1 + i:1)
			i = i + 1
		end while
		while i < 16
			sr_data(2 + i:1) = $20 # ' '
			i = i + 1
		end while

	end if

end

# Starts beeping and flashing when connected to assist in locating collar on
# connection
event connection_status(connection, flags, address, address_type, conn_interval, timeout, latency, bonding)
	# Device is connected.
	if (flags & $05) = $05 then
		state = 1
		call hardware_set_soft_timer(30000, 1, 1)
	end if
end

# Stops beeping and flashing on disconnect
event connection_disconnected(handle, result)

	# Turn off LED and Buzzer
	call hardware_io_port_write(1, $40, 0)
	call hardware_io_port_write(0, $90, 0)
	call hardware_timer_comparator(1, 3, 1, 0)
	state = 1

	# Stop timers
	call hardware_set_soft_timer(0, 1, 1)
	call hardware_set_soft_timer(0, 2, 1)
	call hardware_set_soft_timer(0, 3, 1)

	# Start beaconing again
	call hardware_set_soft_timer(1, 3, 1)

end

